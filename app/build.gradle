apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'detekt.gradle'
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId 'com.smlnskgmail.jaman.githubclient'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0.0'

        buildConfigField 'String', 'API_IMPL', "\"\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    lintOptions {
        abortOnError false
    }

    flavorDimensions 'build'
    productFlavors {
        githubImpl {
            flavorDimensions 'build'

            buildConfigField 'String', 'API_IMPL', "\"GITHUB\""
        }
        fakeImpl {
            flavorDimensions 'build'

            buildConfigField 'String', 'API_IMPL', "\"FAKE\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionName = android.defaultConfig.versionName
            def productFlavorName = variant.flavorName
            def buildTypeName = variant.buildType.name
            output.outputFileName = "android-github-client-${productFlavorName}-${buildTypeName}_${versionName}.apk"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.smlnskgmail.jaman:adaptiverecyclerview:1.1.0'

    // Kodein
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.5.5'

    // Hawk
    implementation "com.orhanobut:hawk:2.0.1"

    // Retrofit
    // noinspection GradleDependency
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'

    // Unit tests
    testImplementation 'junit:junit:4.13'
}
